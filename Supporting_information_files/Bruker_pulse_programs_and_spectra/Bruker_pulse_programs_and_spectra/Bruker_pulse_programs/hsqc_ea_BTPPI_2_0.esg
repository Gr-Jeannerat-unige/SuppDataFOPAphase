;hsqcetgpsp.2
;avance-version (07/04/04)
;HSQC
;2D H-1/X correlation via double inept transfer
;phase sensitive using Echo/Antiecho-TPPI gradient selection
;with decoupling during acquisition
;using trim pulses in inept transfer
;using shaped pulses for inversion and refocussing on f2 - channel
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>

"p2=p1*2"
"p4=p3*2"
"d4=1s/(cnst2*4)"
"d0=3u"
"d10=3u"
"d11=30m"
"d12=3u"
"d13=4u"
"in0=(inf1/2)"
"in10=in0/cnst9"
"cnst22=o2/bf2"
"cnst23=(cnst22-cnst21)*sfo2" ;for ref. to calc.
"cnst24=(-cnst22+cnst21)*sfo2" ;for calc. to ref.
"DELTA1=d4-p16-larger(p2,p14)/2-8u"
"DELTA2=d4-larger(p2,p14)/2"
"DELTA3=d4-30u-larger(p2,p14)/2"
"DELTA=p16+d16"


1 ze
  d11 pl12:f2
2 d1 do:f2 
3 

"cnst30 = 360*(cnst24)*(2*d0 + d13 + d16 + 0.000001*(p3/2 + p2 + p16 + p24/2))"
"cnst31 = 360*(cnst23)*(d13 + 2*d12 + d16 + 0.000001*(p24/2 + p16 + p2 + p3/2))"

  (p1 ph1)
  DELTA2 pl0:f2
  4u
  (center (p2 ph1) (p14:sp3 ph6):f2 ) ;13C phase not recalculated
  4u
  DELTA2*0.5 pl2:f2 UNBLKGRAD 
  DELTA2*0.5 ip9+cnst30 ;NEW PHASE INCREMENTATION
; p28 ph1
; 4u 
  (p1 ph2) 
  (p3 ph3+ph9):f2 ;ph9 calculated towards ph7 - +ph9

; first evolution time - t1
  d0 
  (p2 ph5) 
  d0
  p16:gp1*EA
  d16 pl0:f2
  4u
  (p24:sp7 ph7):f2 ;REFERENCE PHASE
  4u
  DELTA*0.5 pl2:f2
  DELTA*0.5 ip9+cnst31 ;OLD PHASE INCREMENTATION
  3u
  (p2 ph5) 
  3u

  (p3 ph9):f2 ;ph9 calculated towards ph7
  (p1 ph1)  
  DELTA2 pl0:f2
  (center (p2 ph1) (p14:sp3 ph8):f2 ) ;13C phase not recalculated
  4u
  p16:gp2
  DELTA1 pl12:f2
  4u BLKGRAD
  go=2 ph31 cpd2:f2
  d1 do:f2 mc #0 to 2
     F1EA(igrad EA, id0 & ip3*2 & ip6*2 & ip31*2) 
exit
   
ph1=0 
ph2=1
ph3=0 2
ph4=0 0 0 0 2 2 2 2
ph5=0 0 2 2
ph6=0
ph7=0 0 0 0 2 2 2 2
ph8=0
ph9=0 ;incrementation for ph6
ph31=0 2 0 2 2 0 2 0


;pl0 : 120dB
;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl3 : f3 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;sp3: f2 channel - shaped pulse 180 degree for inversion
;sp7: f2 channel - shaped pulse 180 degree for refocussing
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p14: f2 channel - 180 degree shaped pulse for inversion
;p16: homospoil/gradient pulse
;p22: f3 channel - 180 degree high power pulse
;p24: f2 channel - 180 degree shaped pulse for refocussing
;p28: f1 channel - trim pulse
;d0 : incremented delay (2D)                         [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d4 : 1/(4J)XH
;d11: delay for disk I/O                             [30 msec]
;d16: delay for homospoil/gradient recovery
;cnst2: = J(XH)
;inf1: 1/SW(X) = 2 * DW(X)
;in0: 1/(2 * SW(X)) = DW(X)
;nd0: 2
;NS: 1 * n
;DS: >= 16
;td1: number of experiments
;FnMODE: echo-antiecho
;cpd2: decoupling according to sequence defined by cpdprg2
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:	gp 1 : gp 2
;			  80 : 20.1    for C-13
;			  80 :  8.1    for N-15

;for z-only gradients:
;gpz1: 80%
;gpz2: 20.1% for C-13, 8.1% for N-15

;use gradient files:   
;gpnam1: SINE.100
;gpnam2: SINE.100


                                          ;preprocessor-flags-start
;LABEL_CN: for C-13 and N-15 labeled samples start experiment with 
;             option -DLABEL_CN (eda: ZGOPTNS)
                                          ;preprocessor-flags-end



;$Id: hsqcetgpsp.2,v 1.5 2007/04/11 13:34:30 ber Exp $